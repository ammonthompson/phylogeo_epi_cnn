loadPlugin("TensorPhylo", "/home/athompson/software/tensorphylo/build/installer/lib")


# sim_num = 16

num_gen = 10000
thin = 1
symetric=true

# true param treated as known for all inference methods
S_0 = 1000000
recovery_rate = .02667
subsampling_p = 1
num_locations = 5

# true param values for computing error
true_delta = [.00469,.00469,.00469,.00469,.00469]
true_R0 = [3.72434,3.72434,3.72434,3.72434,3.72434]
true_migration = .00017
true_root = 3

# process data
data_file = "tree_files/true_phylogeo_sim16_rep0.location_tree.nexus"

out_file = "output_condition_tree/condition_tree_test_sim16.log"

tree = readTrees(data_file)[1]
taxa = tree.taxa()

data = readDiscreteCharacterData(data_file)
k    = data.getStateDescriptions().size()


# set parameters
age <- tree.rootAge() + tree.branchLength(tree.nnodes())
#age <- tree.rootAge()
moves = VectorMoves()
print("rec rate: " + recovery_rate)
print("subsampleing_p: " + subsampling_p)
print("age: " + age)


# make the tensors
#mu_param ~ dnUniform(0.5 * recovery_rate, 1.5 * recovery_rate)
#moves.append(mvSlide(mu_param))
#mu := rep(mu_param, k)
mu <- rep(recovery_rate, k)


# sampling rate param
delta_param ~ dnUniform(0.0001, 0.005)
#moves.append(mvSlide(delta_param))
moves.append(mvScale(delta_param, weight = 1.25))
delta := rep(delta_param, k)
subsample_delta := delta * subsampling_p
delta_mape := abs(delta[1] - true_delta[1])/true_delta[1] * 100

# beta_M is the beta param in MASTER which = beta/N. 
# Note in dI/dt = beta/N * S * I - gamma * I
# beta/N = beta_M
# R_0 = beta_M * S_0 / gamma = beta/N * S_0 / gamma = beta_M * S_0 / mu
# therefore lambda = beta_M * S_0 = mu * R_0
# if destructive sampling, lambda = (delta + mu) * R_0

R_0_param ~ dnUniform(2,8)
#R_0_param.setValue(4)
moves.append(mvSlide(R_0_param, weight = 1.25))
R_0 := rep(R_0_param , k)
lambda := (delta + mu) * R_0
beta_MASTER := lambda / S_0
R_0_mape := abs(R_0_param - true_R0[1]) / true_R0[1] * 100

# delta, R0 joint move
moves.append(mvUpDownSlide(weight = 1.25))
deltaR0_joint_updown_idx = moves.size()
moves[deltaR0_joint_updown_idx].addVariable(delta_param, true)
moves[deltaR0_joint_updown_idx].addVariable(R_0_param, false)


# mass-event parameters
rho <- 0.00000000000000000001



# anagenetic events
#for(i in 1:choose(k,2)){
#	m[i] ~ dnExponential(1)
#	moves.append(mvScale(m[i]))
#	m[i].setValue(0.01)
#}
#eta := fnFreeSymmetricRateMatrix(m, rescaled = FALSE)

pairwise_migration_rate ~ dnUniform(0.0001, 0.005)
#moves.append(mvSlide(pairwise_migration_rate, weight = 0.5))
moves.append(mvScale(pairwise_migration_rate, weight = 0.25))
migration_mape := abs(pairwise_migration_rate - true_migration) / true_migration * 100

for(i in 1:k){
       for(j in 1:k){
               if( i == j){
                       m[i][j] <- 0

               }else{
			if(symetric){
				if(j > i){
					m[i][j] := pairwise_migration_rate
				}else{
					m[i][j] := m[j][i]
				}
			}else{
				m[i][j] := pairwise_migration_rate
			}
               }
       }
}
eta := fnFreeK(m, rescaled = FALSE)


root_freq <- Simplex(rep(1, k))
#root_freq ~ dnDirichlet(rep(1,k))
#moves.append(mvBetaSimplex(root_freq))
#moves.append(mvElementSwapSimplex(root_freq))



#condition <- "sampled"
#condition <- "time"
##condition <- "survival"
#condition <- "treeExtant"
condition <- "tree"

# distribution object. also called dnGLHBDSP()
psi ~ dnGeneralizedLineageHeterogeneousBirthDeathProcess(
                originAge    = age,
		pi           = root_freq,
                lambda       = lambda,
		eta          = eta,
		mu           = mu,
                delta        = subsample_delta,
		rho          = rho,
		condition    = condition,
                taxa         = taxa,
		nStates	     = k,
		zeroIndex    = TRUE,
		nProc	     = 1
)
psi.clamp(tree)
psi.clampCharData(data)


# model
my_model = model(psi)

# monitors
monitors = VectorMonitors()
monitors.append(mnScreen(printgen=100))
monitors.append(mnModel(printgen=thin, filename=out_file))
monitors.append(mnJointConditionalAncestralState(tree, psi, filename = out_file + ".ancStates", 
		type = "Standard", printgen = 20 * thin, withStartStates = true)) 


# analysis
my_mcmc = mcmc(my_model, monitors, moves)
my_mcmc.burnin(num_gen * 0.2, 100)
my_mcmc.run(num_gen)
q()








